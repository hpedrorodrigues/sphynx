#!/usr/bin/env bash

### Zsh customizations

# http://zsh.sourceforge.net/Doc/Release/Options.html
# https://github.com/ohmyzsh/ohmyzsh/blob/master/lib/history.zsh
setopt extended_history       # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
setopt share_history          # share command history data
setopt inc_append_history     # write commands immediately to the history

# http://zsh.sourceforge.net/Doc/Release/Parameters.html#Parameters-Used-By-The-Shell
export HISTSIZE='1000000000'
export SAVEHIST="${HISTSIZE}"
export HISTFILE="${HOME}/.zsh_history"
export HISTORY_IGNORE='(ls|history|cd|cd -|* --help| *|pwd|exit)'

### Zsh completions
# References:
# - https://github.com/ohmyzsh/ohmyzsh/blob/master/lib/completion.zsh

zmodload -i zsh/complist

export WORDCHARS=''

# http://zsh.sourceforge.net/Doc/Release/Options.html
unsetopt MENU_COMPLETE
setopt AUTO_MENU
setopt COMPLETE_IN_WORD
setopt ALWAYS_TO_END

# should this be in keybindings?
bindkey -M menuselect '^o' accept-and-infer-next-history
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

### Loading external scripts

#|> zsh-defer
export ZSH_DEFER_DIR='/opt/homebrew/share/zsh-defer'

[ -s "${ZSH_DEFER_DIR}/zsh-defer.plugin.zsh" ] \
  && source "${ZSH_DEFER_DIR}/zsh-defer.plugin.zsh"

#|> zsh-autosuggestions
export ZSH_AUTO_SUGGESTIONS_DIR='/opt/homebrew/share/zsh-autosuggestions'

[ -s "${ZSH_AUTO_SUGGESTIONS_DIR}/zsh-autosuggestions.zsh" ] \
  && source "${ZSH_AUTO_SUGGESTIONS_DIR}/zsh-autosuggestions.zsh"

#|> Sphynx
export SPHYNX_DIR="${HOME}/Code/Personal/sphynx"
export SPHYNX_COMPLETION_DIR="${SPHYNX_DIR}/modules/cli/.internal/completion"

if [[ "${PATH}" != *"${SPHYNX_DIR}"* ]]; then
  export PATH="${PATH}:${SPHYNX_DIR}"
fi

if [[ "${fpath}" != *"${SPHYNX_COMPLETION_DIR}"* ]]; then
  # shellcheck disable=SC2206  # Quote to prevent word splitting/globbing
  fpath=("${SPHYNX_COMPLETION_DIR}" ${fpath})
fi

#|> Fuzzy Finder
# Disable CTRL+R shortcut to prevent conflicts with Atuin.
hash fzf 2>/dev/null && FZF_CTRL_R_COMMAND='' eval "$(fzf --zsh)"

#|> Startship
hash starship 2>/dev/null && eval "$(starship init zsh --print-full-init)"

#|> zoxide
hash zoxide 2>/dev/null && eval "$(zoxide init zsh)"

#|> mise
# https://mise.jdx.dev/dev-tools/shims.html#shims-vs-path
hash mise 2>/dev/null && eval "$(mise activate zsh --shims)"

#|> Atuin
hash atuin 2>/dev/null && eval "$(atuin init zsh  --disable-up-arrow)"

#|> Google Cloud SDK
if [ -z "${CLOUDSDK_HOME:-}" ]; then
  gcloud_locations=(
    '/opt/homebrew/Caskroom/google-cloud-sdk/latest/google-cloud-sdk'
    '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk'
    '/snap/google-cloud-sdk/current'
  )

  # shellcheck disable=SC2068  # Double quote array expansions
  for gcloud_location in ${gcloud_locations[@]}; do
    [ -d "${gcloud_location}" ] || continue

    export CLOUDSDK_HOME="${gcloud_location}"
    break
  done

  unset gcloud_location gcloud_locations
fi

# These files take a while to run. Using zsh-defer to postpone their loading.
if [ -n "${CLOUDSDK_HOME:-}" ] && which zsh-defer &>/dev/null; then
  [ -s "${CLOUDSDK_HOME}/path.zsh.inc" ] \
    && zsh-defer source "${CLOUDSDK_HOME}/path.zsh.inc"

  [ -s "${CLOUDSDK_HOME}/completion.zsh.inc" ] \
    && zsh-defer source "${CLOUDSDK_HOME}/completion.zsh.inc"
fi

### Directory shortcuts

# e.g. cd ~p/sphynx
# References:
# - http://zsh.sourceforge.net/Guide
hash -d p="${HOME}/Code/Personal"
hash -d o="${HOME}/Code/Others"
